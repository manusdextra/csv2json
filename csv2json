#!/usr/bin/awk -f
#
# Transforms foobar.csv into foobar.json

BEGIN {
    FS = ","
    printf("[")
}

{
    # trim Windows newlines
    gsub("\r", "")
}

# get column headers from first line
NR == 1 {
    for (i = 0; i < NF; ++i) {
        headers[i] = $(i+1)
    }
}

function quote (value) {
    switch (value) {
    case /^[0-9].*$/:
    case "null":
        return value
        break
    case /True|TRUE/:
        return "true"
    case /False|FALSE/:
        return "false"
    default:
        return sprintf("\"%s\"", value)
        break
    }
}

NR > 1 {
    if (NR > 2) { printf(",") }
    printf("\n\t{")
    for (i = 0; i < NF; ++i) {
      if (i > 0) {printf(", ")}
      key = headers[i]
      value = $(i+1)
      printf(" \"%s\": %s", key, quote(value))
    }
    printf("}")
}

END { printf("\n]") }
